// ************************************************************
// 			  				   EXIT 
// ************************************************************
	var int Pock_ForgetAll;
// ************************************************************

INSTANCE DIA_Pock_EXIT (C_INFO)
{
	npc			= Bau_902_Pock;
	nr			= 999;
	condition	= DIA_Pock_EXIT_Condition;
	information	= DIA_Pock_EXIT_Info;
	permanent	= 1;
	description = DIALOG_ENDE;
};                       

FUNC INT DIA_Pock_EXIT_Condition()
{
	return 1;
};

FUNC VOID DIA_Pock_EXIT_Info()
{	
	Pock_ForgetAll = TRUE;
	AI_StopProcessInfos	(self);
};

// *************************************************************************
// 									WASSER
// *************************************************************************

INSTANCE Info_Pock_Wasser(C_INFO) // E1
{
	npc			= Bau_902_Pock;
	nr			= 800;
	condition	= Info_Pock_Wasser_Condition;
	information	= Info_Pock_Wasser_Info;
	permanent	= 1;
	description = "Mi manda Lefty. Ti ho portato un po' d'acqua.";
};                       

FUNC INT Info_Pock_Wasser_Condition()
{
	if	((Lefty_Mission==LOG_RUNNING) || ((Lefty_Mission==LOG_SUCCESS) && Npc_HasItems(other, ItFo_Potion_Water_01)))
	&&	(self.aivar[AIV_DEALDAY] <= Wld_GetDay())
	{
		return 1;
	};
};

FUNC VOID Info_Pock_Wasser_Info()
{
	AI_Output(other,self,"Info_Pock_Wasser_15_00"); //Mi manda Lefty. Ti ho portato un po' d'acqua.
	if (Npc_HasItems(other, ItFo_Potion_Water_01)>=1)
	{
		B_GiveInvItems(other,self,ItFo_Potion_Water_01,1);
		if ( C_BodystateContains(self, BS_SIT) )
		{
			AI_StandUp		(self);
			AI_TurnToNpc	(self, hero);
		};
		AI_UseItem(self, ItFo_Potion_Water_01);
		AI_Output(self,other,"Info_Pock_Wasser_04_01"); //Grazie, amico! Ne avevo davvero bisogno!
		An_Bauern_verteilt = An_Bauern_verteilt+1;
		if (An_Bauern_verteilt>=DurstigeBauern)
		{
			Lefty_Mission = LOG_SUCCESS;
		};
		self.aivar[AIV_DEALDAY] = Wld_GetDay()+1;
	}
	else
	{
		AI_Output(self,other,"Info_Pock_Wasser_NOWATER_04_00"); //Ma non ne hai più! Non fa nulla, amico. Chiederò a qualcun altro.
	};
};

// ************************************************************
// 						Hallo
// ************************************************************

INSTANCE DIA_Pock_Hello (C_INFO)
{
	npc			= Bau_902_Pock;
	nr			= 1;
	condition	= DIA_Pock_Hello_Condition;
	information	= DIA_Pock_Hello_Info;
	permanent	= 0;
	description	= "Salve! Sono nuovo di qui.";
};                       

FUNC INT DIA_Pock_Hello_Condition()
{
	if (Pock_ForgetAll == FALSE)
	{
		return 1;
	};
};

FUNC VOID DIA_Pock_Hello_Info()
{	
	AI_Output (other, self,"DIA_Pock_Hello_15_00"); //Salve! Sono nuovo di qui.
	AI_Output (self, other,"DIA_Pock_Hello_04_01"); //È bello vedere un volto nuovo.
	AI_Output (other, self,"DIA_Pock_Hello_15_02"); //Devi essere qui da parecchio, vero?
	AI_Output (self, other,"DIA_Pock_Hello_04_03"); //Hai maledettamente ragione, ragazzo. Io sono stato uno dei primi.
};

// ************************************************************
// 						Weißt ne Menge
// ************************************************************

INSTANCE DIA_Pock_KnowMuch (C_INFO)
{
	npc			= Bau_902_Pock;
	nr			= 1;
	condition	= DIA_Pock_KnowMuch_Condition;
	information	= DIA_Pock_KnowMuch_Info;
	permanent	= 0;
	description	= "Quindi saprai molte cose su questo posto!";
};                       

FUNC INT DIA_Pock_KnowMuch_Condition()
{
	if ( (Pock_ForgetAll==FALSE) && Npc_KnowsInfo(hero,DIA_Pock_Hello) )
	{
		return 1;
	};
};

FUNC VOID DIA_Pock_KnowMuch_Info()
{	
	AI_Output (other, self,"DIA_Pock_KnowMuch_15_00"); //Quindi saprai molte cose su questo posto!
	AI_Output (self, other,"DIA_Pock_KnowMuch_04_01"); //Insomma... Passo la maggior parte del tempo qui, a raccogliere riso.
	AI_Output (self, other,"DIA_Pock_KnowMuch_04_02"); //Ecco perché sono invecchiato così tanto. Tutto quello che riceviamo è un po' di riso e un goccio di grappa ogni tanto. Non è molto, ma ci accontentiamo.
};

// ************************************************************
// 							Warum hier?
// ************************************************************

INSTANCE DIA_Pock_WhyJail (C_INFO)
{
	npc			= Bau_902_Pock;
	nr			= 2;
	condition	= DIA_Pock_WhyJail_Condition;
	information	= DIA_Pock_WhyJail_Info;
	permanent	= 0;
	description	= "Perché sei qui?";
};                       

FUNC INT DIA_Pock_WhyJail_Condition()
{
	if ( (Pock_ForgetAll==FALSE) && Npc_KnowsInfo(hero,DIA_Pock_Hello) )
	{
		return 1;
	};
};

FUNC VOID DIA_Pock_WhyJail_Info()
{	
	AI_Output (other, self,"DIA_Pock_WhyJail_15_00"); //Perché sei qui?
	AI_Output (self, other,"DIA_Pock_WhyJail_04_01"); //Tasse, ragazzo, tasse! Il mio silos era vuoto come il mio stomaco e non potevo pagare.
	AI_Output (self, other,"DIA_Pock_WhyJail_04_02"); //Un giorno sono venuti i soldati e mi hanno portato qui. In ogni caso, non è poi così peggio che fuori. Almeno non morirò di fame!
	AI_Output (self, other,"DIA_Pock_WhyJail_04_03"); //Avevo sempre pagato regolarmente per tutta la vita. Ma questo non era sufficiente per quel figlio di puttana con la corona!
};

// ************************************************************
// 						PERM (Forget)
// ************************************************************

INSTANCE DIA_Pock_ForgotAll (C_INFO)
{
	npc			= Bau_902_Pock;
	nr			= 1;
	condition	= DIA_Pock_ForgotAll_Condition;
	information	= DIA_Pock_ForgotAll_Info;
	permanent	= 1;
	description	= "Tutto a posto?";
};                       

FUNC INT DIA_Pock_ForgotAll_Condition()
{
	if (Pock_ForgetAll == TRUE)
	{
		return 1;
	};
};

FUNC VOID DIA_Pock_ForgotAll_Info()
{	
	AI_Output (other, self,"DIA_Pock_ForgotAll_15_00"); //Tutto a posto?
	AI_Output (self, other,"DIA_Pock_ForgotAll_04_01"); //È bello vedere un volto nuovo.
	AI_Output (other, self,"DIA_Pock_ForgotAll_15_02"); //Aspetta un attimo! Abbiamo già parlato.
	AI_Output (self, other,"DIA_Pock_ForgotAll_04_03"); //No, non è possibile. Non ti ho mai visto prima.
	AI_Output (other, self,"DIA_Pock_ForgotAll_15_04"); //Certo...
};

