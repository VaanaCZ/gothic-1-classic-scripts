// **************************************************
// 						 EXIT 
// **************************************************

INSTANCE DIA_Melvin_Exit (C_INFO)
{
	npc			= Vlk_582_Melvin;
	nr			= 999;
	condition	= DIA_Melvin_Exit_Condition;
	information	= DIA_Melvin_Exit_Info;
	permanent	= 1;
	description = DIALOG_ENDE;
};                       

FUNC INT DIA_Melvin_Exit_Condition()
{
	return 1;
};

FUNC VOID DIA_Melvin_Exit_Info()
{
	AI_StopProcessInfos	( self );
};

// **************************************************
// 				Was machst du?
// **************************************************

INSTANCE DIA_Melvin_Hello (C_INFO)
{
	npc				= Vlk_582_Melvin;
	nr				= 1;
	condition		= DIA_Melvin_Hello_Condition;
	information		= DIA_Melvin_Hello_Info;
	permanent		= 0;
	description		= "Salve! Sei di questo campo, vero?";
};

FUNC INT DIA_Melvin_Hello_Condition()
{	
	return 1;
};

FUNC VOID DIA_Melvin_Hello_Info()
{
	AI_Output (other, self,"DIA_Melvin_Hello_15_00"); //Salve! Sei di questo campo, vero?
	AI_Output (self, other,"DIA_Melvin_Hello_03_01"); //Ora sì! Ho lasciato Campo Vecchio una settimana fa. Ho chiesto al mio amico Dusty di venire con me, ma ha preferito aspettare.

	Log_CreateTopic		(CH1_RecruitDusty,	LOG_MISSION);
	Log_SetTopicStatus	(CH1_RecruitDusty,	LOG_RUNNING);
	B_LogEntry			(CH1_RecruitDusty,"Melvin il minatore si è unito alla Fratellanza. Il suo amico Dusty è ancora a Campo Vecchio, ma ha intenzione d'andarsene.");
};

// **************************************************
// 				Dusty getroffen
// **************************************************

INSTANCE DIA_Melvin_MetDusty (C_INFO)
{
	npc				= Vlk_582_Melvin;
	nr				= 1;
	condition		= DIA_Melvin_MetDusty_Condition;
	information		= DIA_Melvin_MetDusty_Info;
	permanent		= 0;
	description		= "Conosco Dusty. Ho scambiato due parole con lui.";
};

FUNC INT DIA_Melvin_MetDusty_Condition()
{	
	if ( (Npc_KnowsInfo(hero,DIA_Melvin_Hello)) && (Npc_KnowsInfo(hero,DIA_Dusty_Hello)) )
	{
		return 1;
	};
};

FUNC VOID DIA_Melvin_MetDusty_Info()
{
	AI_Output (other, self,"DIA_Melvin_MetDusty_15_00"); //Conosco Dusty. Ho scambiato due parole con lui.
	AI_Output (self, other,"DIA_Melvin_MetDusty_03_01"); //Se lo dovessi incontrare ancora, digli che è stato uno stupido a restarsene là. Qui è dieci volte meglio che essere maltrattati dalle guardie di Campo Vecchio.
	AI_Output (other, self,"DIA_Melvin_MetDusty_15_02"); //Glielo farò sapere, se dovessi rivederlo.
};


